apply plugin: 'com.android.library'

android {
    compileSdkVersion 24
    buildToolsVersion "24.0.1"

    defaultConfig {
        minSdkVersion 14
        targetSdkVersion 24
        versionCode 1
        versionName libraryVersion
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    lintOptions {
        abortOnError true
        absolutePaths false
        lintConfig rootProject.file("lint.xml")
    }
}

apply plugin: 'checkstyle'

checkstyle {
    toolVersion '6.0'
}


task checkstyle(type: Checkstyle) {
    group 'verification'
    configFile rootProject.file('checkstyle.xml')
    source = ['src/main/java', 'src/test/java']

    ignoreFailures false
    showViolations true
    include '**/*.java'
    exclude '**/R.java'
    exclude '**/BuildConfig.java'

    classpath = files()
    configProperties = [
            'proj.module.dir'      : projectDir.absolutePath,
            'checkstyle.cache.file': './build/cache/checkstyle-cache'
    ]
}

afterEvaluate {
    if (project.tasks.findByName('check')) {
        check.dependsOn('checkstyle')
    }
}

dependencies {
    compile 'com.android.support:appcompat-v7:24.1.1'
    compile "com.andretietz.retroauth:retroauth-core:$libraryVersion"

    testCompile 'junit:junit:4.12'
    testCompile "org.robolectric:robolectric:3.1.2"
    testCompile 'org.mockito:mockito-core:2.0.90-beta'

}
apply plugin: 'com.getkeepsafe.dexcount'
apply plugin: 'com.github.ben-manes.versions'
apply from: 'jfrog-publish.gradle'
apply from: 'bintray-publish.gradle'