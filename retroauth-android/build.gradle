apply plugin: 'com.android.library'

repositories {
    mavenLocal()
    jcenter()
    maven { url('https://oss.jfrog.org/artifactory/oss-snapshot-local/') }
}

android {
    compileSdkVersion 23
    buildToolsVersion "23.0.3"

    defaultConfig {
        minSdkVersion 14
        targetSdkVersion 23
        versionCode 1
        versionName libraryVersion
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    lintOptions {
        abortOnError true
        absolutePaths false
        lintConfig file("lint.xml")
    }
}

apply plugin: 'checkstyle'

checkstyle {
    toolVersion '6.0'
}


task checkstyle(type: Checkstyle) {
    group 'verification'
    configFile rootProject.file('checkstyle.xml')
    source = ['src/main/java', 'src/test/java']

    ignoreFailures false
    showViolations true
    include '**/*.java'
    exclude '**/R.java'
    exclude '**/BuildConfig.java'

    classpath = files()
    configProperties = [
            'proj.module.dir'      : projectDir.absolutePath,
            'checkstyle.cache.file': './build/cache/checkstyle-cache'
    ]
}

afterEvaluate {
    if (project.tasks.findByName('check')) {
        check.dependsOn('checkstyle')
    }
}

configurations.all { resolutionStrategy.cacheChangingModulesFor 0, 'seconds' }

dependencies {
    compile 'com.android.support:appcompat-v7:23.3.0'
    compile ("com.andretietz.retroauth:retroauth-core:$libraryVersion") {
        changing = true
    }
    testCompile 'junit:junit:4.12'
    testCompile 'org.mockito:mockito-core:2.0.31-beta'

}
/**
 * Before release:
 * - remove gradle.properties speed up config (pom is not generated otherwise for some reason)
 * - make sure the core project is not in release mode
 * /
apply from: 'jfrog-publish.gradle'
//*/