/*
 * Copyright 2013 Chris Banes
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

apply plugin: 'maven'
apply plugin: 'signing'

group = GROUP
version = VERSION_NAME


def isReleaseBuild() {
    return !VERSION_NAME.contains("SNAPSHOT")
}

def getRepositoryUsername() {
    return project.hasProperty('bintrayUser') ? project.bintrayUser : System.getenv("BINTRAY_USER") ?: ""
}

def getRepositoryPassword() {
    return project.hasProperty('bintrayKey') ? project.bintrayKey : System.getenv("BINTRAY_PASSWORD") ?: ""
}

afterEvaluate { project ->
    uploadArchives {
        repositories {
            mavenDeployer {
                beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

                pom.groupId = GROUP
                pom.artifactId = POM_ARTIFACT_ID
                pom.version = VERSION_NAME
                repository(url: "file://" + System.getProperty("user.home") + "/.m2/repository/") {
//                repository(url: "https://api.bintray.com/maven/unicate/android/retroauth/;publish=1") {
                    authentication(userName: getRepositoryUsername(), password: getRepositoryPassword())
                }
//                snapshotRepository(url: "file://" + System.getProperty("user.home") + "/.m2/repository/") {
                snapshotRepository(url: "https://oss.jfrog.org/artifactory/oss-snapshot-local/") {
                    authentication(userName: getRepositoryUsername(), password: getRepositoryPassword())
                }

                pom.project {
                    name POM_NAME
                    packaging POM_PACKAGING
                    description POM_DESCRIPTION
                    url POM_URL

                    scm {
                        url POM_SCM_URL
                        connection POM_SCM_CONNECTION
                        developerConnection POM_SCM_DEV_CONNECTION
                    }

                    licenses {
                        license {
                            name POM_LICENCE_NAME
                            url POM_LICENCE_URL
                            distribution POM_LICENCE_DIST
                        }
                    }

                    developers {
                        developer {
                            id POM_DEVELOPER_ID
                            name POM_DEVELOPER_NAME
                        }
                    }
                }
            }
        }
    }

    signing {
        required { isReleaseBuild() && gradle.taskGraph.hasTask("uploadArchives") }
        sign configurations.archives
    }

    task androidJavadocs(type: Javadoc) {
        if(!project.plugins.hasPlugin('java')) {
            if (!project.plugins.hasPlugin('kotlin')) {
                if (!project.plugins.hasPlugin('kotlin-android')) {
                    source = android.sourceSets.main.java.srcDirs
                }
                classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
            }
        }
        exclude '**/internal/*'
        if (JavaVersion.current().isJava8Compatible()) {
            options.addStringOption('Xdoclint:none', '-quiet')
        }
    }

    task androidJavadocsJar(type: Jar, dependsOn: androidJavadocs) {
        classifier = 'javadoc'
        from androidJavadocs.destinationDir
    }

    task androidSourcesJar(type: Jar) {
        classifier = 'sources'
        if (project.plugins.hasPlugin('kotlin-android')) {
            from android.sourceSets.main.java.sourceFiles
        }
    }

    artifacts {
        archives androidSourcesJar
        archives androidJavadocsJar
    }
}